'$DYNAMIC
'$INCLUDE: 'tp.h'
DEFINT A-Z
'-----------------------------------------------------------------------------
'                     QBPAINT V4.0 1995 THE S.H. SOFTWORX
'-----------------------------------------------------------------------------

TYPE HUES
 RED AS INTEGER
 GRN AS INTEGER
 BLU AS INTEGER
END TYPE

DECLARE SUB CHK.XY ()
DECLARE SUB DISKFUNC (MD%)
DECLARE SUB ARRANGE ()
DECLARE SUB OPTIONS (MD%)
DECLARE SUB TOOLFUNC (MD%)
DECLARE SUB DRAWBORDERS (c%)
DECLARE SUB PASTEFROM ()
DECLARE SUB QUICKCOPY (SPM%)
DECLARE SUB ZOOMEDIT ()
DECLARE SUB CLICKWAIT ()
DECLARE SUB INTIALIZE ()
DECLARE SUB RESETMOUSE ()
DECLARE SUB PAINTFUNC (MD%)
DECLARE SUB COPYFUNC (MD%)
DECLARE SUB DRAWFUNC (MD%)
DECLARE SUB PALETTEFUNC (MD%)
DECLARE SUB FINDCOLOR (MD%)
DECLARE SUB SHOWMENU ()
DECLARE SUB EDITPALETTE ()
DECLARE SUB SELECTPOINT (MD%)
DECLARE SUB SELECTAREA (MW%, MH%)
DECLARE SUB SPRCONFIGS (MD%, MD1%)
DECLARE SUB MOUSEFUNC (MD%, X1%, X2%)
DECLARE SUB UPDATEPAL (Mode%, ST%, ET%)
DECLARE SUB PASTE (SOURCE%(), MD%)
DECLARE SUB SETHUES (HUES() AS HUES, SC%, EC%)

DECLARE FUNCTION ASK% (Q$)
DECLARE FUNCTION INPUTNUM% (M$, L%)
DECLARE FUNCTION INPUTNAME$ (FSPC$)
DECLARE FUNCTION PDMENU% (MS%, ML%)
DECLARE FUNCTION FILECHECK% (F$, MD%)

COMMON SHARED MDAT$, MX, MY, CLEFT, CRGHT, WKIMG(), OPT()
COMMON SHARED Pal() AS HUES, CLR, SW, SH, XSTP, YSTP, XST, O1R
COMMON SHARED YST, MXI, MYI, MENUX, MENUY, SMX, SMY, SX, SY
COMMON SHARED EX, EY, EXT, MENU$(), SPR(), FS&, SN, SSW, SSH

INIT = WHICHVGA
RESOK = RES320
SCREEN 13
EXT = 1: INTIALIZE

RESETMOUSE
MOUSEFUNC 1, 0, 0

DO
 DO
  MOUSEFUNC 3, 0, 0: K$ = UCASE$(INKEY$)
  IF K$ = CHR$(27) THEN
   MOUSEFUNC 2, 0, 0: Q = ASK(" QUIT QBP? (Y/N) ")
   IF Q THEN
    CLS : SYSTEM
   ELSE
    PUT (0, 0), WKIMG, PSET
   END IF
   MOUSEFUNC 1, 0, 0
  END IF
  IF K$ = "1" OR K$ = "2" OR K$ = "3" THEN SPM = VAL(K$) - 1
  IF CRGHT THEN
   CLICKWAIT
   IF OPT(1) THEN
    QUICKCOPY SPM
   ELSE
    MOUSEFUNC 2, 0, 0: CLR = POINT(MX, MY): MOUSEFUNC 1, 0, 0
   END IF
  END IF
  CHK.XY
 LOOP UNTIL CLEFT OR EXT
 EXT = 0: CLICKWAIT
 MOUSEFUNC 2, 0, 0: SMX = (MX * 2): SMY = MY
 SPRCONFIGS 0, 1: SHOWMENU: MOUSEFUNC 1, 0, 0
 DO
  MOUSEFUNC 3, 0, 0
  K$ = UCASE$(INKEY$)
  IF K$ <> "" THEN
   MOUSEFUNC 2, 0, 0: EXIT DO
  END IF
  IF CRGHT THEN
   CLICKWAIT
   MOUSEFUNC 2, 0, 0: EXIT DO
  END IF
  IF CLEFT THEN
   CLICKWAIT
   MOUSEFUNC 2, 0, 0: MENUX = MX: MENUY = MY
   IF MX > 150 THEN
    CLICKWAIT
    FINDCOLOR 0: CLR = POINT(MX, MY)
    FINDCOLOR 1: MOUSEFUNC 1, 0, 0
   END IF
   SELECT CASE MY
    CASE 71 TO 84
     SELECT CASE MX
      CASE 73 TO 91        'PD DRAW
       IF OPT(1) = 0 THEN
         SLCT = PDMENU(0, 5): IF EXT THEN EXIT DO
         DRAWFUNC SLCT: EXIT DO
       END IF
      CASE 93 TO 112       'PD COPY
       SLCT = PDMENU(5, 6): IF EXT THEN EXIT DO
       COPYFUNC SLCT: EXIT DO
      CASE 114 TO 133      'PD PAINT
       SLCT = PDMENU(11, 3): IF EXT THEN EXIT DO
       PAINTFUNC SLCT: EXIT DO
      CASE 136 TO 146
       CLR = CLR - 64
       IF CLR < 0 THEN CLR = CLR + 64
       FINDCOLOR 1
     END SELECT
    CASE 86 TO 100
     SELECT CASE MX
      CASE 73 TO 91        'PD ZOOM
       ZOOMEDIT
       EXIT DO
      CASE 93 TO 112       'PD TOOL
       SLCT = PDMENU(14, 5): IF EXT THEN EXIT DO
       TOOLFUNC SLCT: EXIT DO
      CASE 114 TO 133      'PD PALETTE
       SLCT = PDMENU(19, 4): IF EXT THEN EXIT DO
       PALETTEFUNC SLCT: EXIT DO
      CASE 136 TO 146
       PUT (0, 0), WKIMG, PSET
       RP = ASK(" RESET PALETTE? (Y/N) ")
       IF RP THEN
        DEF SEG = VARSEG(Pal(0))
        BLOAD "QBP001.DAT", 0
        SETHUES Pal(), 0, 255
       END IF
       EXIT DO
     END SELECT
    CASE 102 TO 115
     SELECT CASE MX
      CASE 73 TO 91        'PD OPTION
       SLCT = PDMENU(23, 6): IF EXT THEN EXIT DO
       OPTIONS SLCT: EXIT DO
      CASE 93 TO 112       'PD DISK
       SLCT = PDMENU(29, 5): IF EXT THEN EXIT DO
       DISKFUNC SLCT: EXIT DO
      CASE 114 TO 133      'EXIT
       CLS : SYSTEM
      CASE 136 TO 146
       CLR = CLR + 64
       IF CLR > 255 THEN CLR = CLR - 64
       FINDCOLOR 1
     END SELECT
    END SELECT
   MOUSEFUNC 1, 0, 0
  END IF
 LOOP UNTIL EXT
 EXT = 0
 RESETMOUSE
 PUT (0, 0), WKIMG, PSET
 IF OPT(1) THEN
  DRAWBORDERS 120: GET (0, 0)-(319, 199), WKIMG
 END IF
 MOUSEFUNC 1, 0, 0
LOOP

NFF:
 PRINT
 PRINT "NO FILES FOUND..."
RESUME NEXT

FNF1:
RESUME NEXT

REM $STATIC
SUB ARRANGE

 DO
  IF OPT(1) = 0 THEN
   SELECTPOINT 1: IF EXT THEN EXIT SUB
   SELECTAREA 320, 200: IF EXT THEN EXIT SUB
  ELSE
   SELECTPOINT 1: IF EXT THEN EXIT SUB
   SPRCONFIGS 0, 0
  END IF
  PUT (0, 0), WKIMG, PSET
  IF MD <> 5 THEN
   W& = (EX + 1) - SX: H& = (EY + 1) - SY
   CS& = (W& * H&) \ 2 + 2: IF CS& > 27002 THEN EXIT SUB
   REDIM CLPBRD(CS&): GET (SX, SY)-(EX, EY), CLPBRD
  END IF
  IF OPT(1) = 0 THEN
   MOUSEFUNC 8, 0, (200 - H&): MOUSEFUNC 7, 0, (320 * 2) - (W& * 2)
  END IF
  LINE (SX, SY)-(EX, EY), 0, BF
  GET (0, 0)-(319, 199), WKIMG
  PASTE CLPBRD(), 0
 LOOP UNTIL EXT
 
END SUB

FUNCTION ASK (Q$)

 REDIM TMP(1282): GET (0, 0)-(319, 7), TMP

 LOCATE 1, 21 - LEN(Q$) \ 2: PRINT Q$

 DO
  MOUSEFUNC 3, 0, 0
  K$ = UCASE$(INKEY$)
 LOOP UNTIL K$ = "Y" OR K$ = "N" OR CLEFT OR CRGHT

 PUT (0, 0), TMP, PSET: ERASE TMP

 IF CLEFT OR K$ = "Y" THEN ASK = 1 ELSE ASK = 0

 CLICKWAIT

END FUNCTION

SUB CHK.XY

 IF OPT(3) THEN
  LOCATE 1, 15: PRINT "X:"; MX: LOCATE 1, 22: PRINT "Y:"; MY
 END IF

END SUB

SUB CLICKWAIT

 DO: MOUSEFUNC 3, 0, 0: LOOP WHILE CLEFT OR CRGHT

END SUB

SUB COPYFUNC (MD)

 IF OPT(1) = 0 THEN
  SELECTPOINT 1: IF EXT THEN EXIT SUB
  SELECTAREA 320, 200: IF EXT THEN EXIT SUB
 END IF

 PUT (0, 0), WKIMG, PSET

 IF MD <> 6 THEN
  W& = (EX + 1) - SX: H& = (EY + 1) - SY
  CS& = (W& * H&) \ 2 + 2: IF CS& > 27002 THEN EXIT SUB
  REDIM CLPBRD(CS&): GET (SX, SY)-(EX, EY), CLPBRD
 END IF

 IF OPT(1) = 0 THEN
  MOUSEFUNC 8, 0, (200 - H&): MOUSEFUNC 7, 0, (320 * 2) - (W& * 2)
 END IF

 SELECT CASE MD
  CASE 1 TO 3
   PASTE CLPBRD(), MD - 1
  CASE 4
   DO: PASTE CLPBRD(), 0: LOOP UNTIL EXT
  CASE 5
   LINE (SX, SY)-(EX, EY), 0, BF
   GET (0, 0)-(319, 199), WKIMG
   PASTE CLPBRD(), 0
   IF EXT = 0 THEN
    IF OPT(1) THEN
     SELECTPOINT 0: IF EXT THEN EXIT SUB
     SPRCONFIGS 0, 0
    END IF
    ERASE CLPBRD
   END IF
  CASE 6
   LINE (SX, SY)-(EX, EY), 0, BF
   GET (0, 0)-(319, 199), WKIMG
 END SELECT

 ERASE CLPBRD

END SUB

SUB DISKFUNC (MD)

 SELECT CASE MD
  CASE 1
   IT = PDMENU(34, 2): IF EXT THEN EXIT SUB
   PUT (0, 0), WKIMG, PSET
   SELECT CASE IT
    CASE 1
     F$ = INPUTNAME$("*.QBI"): IF EXT THEN EXIT SUB
     FF = FILECHECK(F$, 0)
     IF FF THEN
      CLS : OPT(1) = 0
      REDIM WKIMG((FS& - 7) \ 2 - 1)
      DEF SEG = VARSEG(WKIMG(0)): BLOAD F$, 0
      F$ = LEFT$(F$, LEN(F$) - 3) + "PAL": FP = FILECHECK(F$, 0)
      IF FP THEN
       DEF SEG = VARSEG(Pal(0)): BLOAD F$, 0: SETHUES Pal(), 0, 255
      END IF
      'BLKPUT 1, 0, 0, WKIMG(0)
      PUT (0, 0), WKIMG, PSET
     ELSE
      N = FILECHECK("", 1): EXIT SUB
     END IF
    CASE 2
     F$ = INPUTNAME$("*.QBS"): IF EXT THEN EXIT SUB
     FF = FILECHECK(F$, 0)
     IF FF THEN
      OPT(1) = 1: REDIM WKIMG((FS& - 7) \ 2 - 1)
      DEF SEG = VARSEG(WKIMG(0)): BLOAD F$, 0
      SFS& = (FS& - 7) \ 2 - 1
      F$ = LEFT$(F$, LEN(F$) - 3) + "PAL": FP = FILECHECK(F$, 0)
      IF FP THEN
       DEF SEG = VARSEG(Pal(0)): BLOAD F$, 0: SETHUES Pal(), 0, 255
      END IF
      SW = WKIMG(0) \ 8: SH = WKIMG(1)
      SPRCONFIGS 1, 0: SS& = (SW * SH) \ 2 + 2
      X = XST + 1: Y = YST + 1: R = 1: CLS
      DO
       PUT (X, Y), WKIMG(N), PSET
       N = N + SS&: X = X + XSTP: R = R + 1
       IF R > MXI THEN X = XST + 1: Y = Y + YSTP: R = 1
      LOOP WHILE N < SFS&
     ELSE
      N = FILECHECK("", 1): EXIT SUB
     END IF
   END SELECT
  CASE 2
   IT = PDMENU(34, 2): IF EXT THEN EXIT SUB
   PUT (0, 0), WKIMG, PSET
   SELECT CASE IT
    CASE 1
     SELECTPOINT 1: IF EXT THEN EXIT SUB
     SELECTAREA 320, 200: IF EXT THEN EXIT SUB
     F$ = INPUTNAME$("*.QBI"): IF EXT THEN EXIT SUB
     FF = FILECHECK(F$, 0): OV = 1
     IF FF THEN
      LINE (0, 0)-(319, 7), 0, BF: OV = ASK(" OVERWRITE? (Y/N) ")
     END IF
     IF OV THEN
      PUT (0, 0), WKIMG, PSET
      W& = (EX + 1) - SX: H& = (EY + 1) - SY
      SS& = (W& * H&) \ 2 + 2: REDIM WKIMG(SS&)
      'BLKGET SX, SY, EX, EY, WKIMG(0)
      GET (SX, SY)-(EX, EY), WKIMG
      DEF SEG = VARSEG(WKIMG(0)): BSAVE F$, 0, ((SS& + 1) * 2&)
      SP = ASK(" SAVE PALETTE? (Y/N) ")
      IF SP THEN
       F$ = LEFT$(F$, LEN(F$) - 3) + "PAL"
       DEF SEG = VARSEG(Pal(0)): BSAVE F$, 0, 1536
      END IF
     ELSE
      EXIT SUB
     END IF
    
     'F$ = INPUTNAME$("*.QBI"): IF EXT THEN EXIT SUB
     'FF = FILECHECK(F$, 0): OV = 1
     'IF FF THEN
     ' LINE (0, 0)-(319, 7), 0, BF: OV = ASK(" OVERWRITE? (Y/N) ")
     'END IF
     'IF OV THEN
     ' PUT (0, 0), WKIMG, PSET
     ' REDIM WKIMG(32001)
     ' BLKGET 0, 0, 319, 199, WKIMG(0)         ' ******** SAVE IMAGE *********
     ' DEF SEG = VARSEG(WKIMG(0)): BSAVE F$, 0, 64002
     ' SP = ASK(" SAVE PALETTE? (Y/N) ")
     ' IF SP THEN
     '  F$ = LEFT$(F$, LEN(F$) - 3) + "PAL"
     '  DEF SEG = VARSEG(Pal(0)): BSAVE F$, 0, 1536
     ' END IF
     ' REDIM WKIMG(32001): GET (0, 0)-(319, 199), WKIMG(0)
     'ELSE
     ' EXIT SUB
     'END IF
    CASE 2
     IF OPT(1) = 0 THEN EXIT SUB
     REDIM TMP(1282): GET (0, 0)-(319, 7), TMP
     SELECTPOINT 1: IF EXT THEN EXIT SUB
     SPRCONFIGS 0, 1: X = XST: Y = YST: R = 1: SS& = (SW * SH) \ 2 + 2
     FOR i = 0 TO SN
      LINE (X, Y)-(X + XSTP, Y + YSTP), 112, B
      X = X + XSTP: R = R + 1
      IF R > MXI THEN R = 1: X = XST: Y = Y + YSTP
     NEXT
     F$ = INPUTNAME$("*.QBS"): IF EXT THEN EXIT SUB
     FF = FILECHECK(F$, 0): OV = 1
     IF FF THEN
      LINE (0, 0)-(319, 7), 0, BF: OV = ASK(" OVERWRITE? (Y/N) ")
     END IF
     IF OV THEN
      PUT (0, 0), WKIMG, PSET: SPRCONFIGS 2, 0
      TOT& = (SN + 1) * SS&: LINE (0, 0)-(319, 7), 0, BF
      DEF SEG = VARSEG(SPR(0)): BSAVE F$, 0, (TOT& * 2&)
      SP = ASK(" SAVE PALETTE? (Y/N) ")
      IF SP THEN
       F$ = LEFT$(F$, LEN(F$) - 3) + "PAL"
       DEF SEG = VARSEG(Pal(0)): BSAVE F$, 0, 1536
      END IF
      PUT (0, 0), TMP, PSET: ERASE SPR, TMP
     ELSE
      EXIT SUB
     END IF
   END SELECT
  CASE 3
   IT = PDMENU(34, 2): IF EXT THEN EXIT SUB
   SELECT CASE IT
     CASE 1: FSPC$ = "*.QBI": CASE 2: FSPC$ = "*.QBS"
   END SELECT
   F$ = INPUTNAME$(FSPC$): IF EXT THEN EXIT SUB
   FF = FILECHECK(F$, 0)
   IF FF THEN
    KILL F$: PUT (0, 0), WKIMG, PSET
   ELSE
    N = FILECHECK("", 1): EXIT SUB
   END IF
  CASE 4
   PUT (0, 0), WKIMG, PSET
   F$ = INPUTNAME$("*.BMP"): IF EXT THEN EXIT SUB
   FF = FILECHECK(F$, 0)
   IF FF THEN
    CLS : OPT(1) = 0: PF& = 0: T = 0
    OPEN F$ FOR BINARY AS #1
     GET #1, 19, W&: GET #1, 23, H&
     IF W& > 320 OR H& > 200 THEN CLOSE #1: EXIT SUB
     P& = 0: F& = 55: TMR = W& * H& / 100
     DO
      FOR T = 1 TO 3
       GET #1, F&, DAT: DAT1 = DAT / 4 MOD 64
       IF DAT1 < 0 THEN
        DO
         DAT1 = 64 + DAT1
        LOOP UNTIL DAT1 >= 0 OR DAT1 <= 64
       END IF
       SELECT CASE T
        'CASE 1: Pal(P&).BLU = DAT1
        'CASE 2: Pal(P&).GRN = DAT1
        'CASE 3: Pal(P&).RED = DAT1
       END SELECT
       F& = F& + 1
      NEXT
      P& = P& + 1: F& = F& + 1
     LOOP WHILE P& < 256
     STP = (320 - W&) MOD 4: F& = FS& - STP
     LS& = (W& * H&) \ 2 + 2: REDIM WKIMG(LS&)
     GET (0, 0)-(W& - 1, H& - 1), WKIMG: P& = 4
     FOR H1 = 1 TO H&: PP& = P& + W& - 1: FOR W1 = 1 TO W&
      GET #1, F&, DAT: DEF SEG = VARSEG(WKIMG(0))
      POKE PP&, DAT: T = T + 1: IF T >= TMR THEN T = 0: PF = PF + 1
      F& = F& - 1: PP& = PP& - 1
     NEXT: F& = F& - STP: P& = P& + W&
     LOCATE 12, 13: PRINT "PROCESSING"; PF; "%"
     NEXT
     CLOSE #1
     CLS : SETHUES Pal(), 0, 255: PUT (0, 0), WKIMG, PSET
     DST = ASK(" DESTROY 'BMP' FILE? (Y/N) "): IF DST THEN KILL F$
    ELSE
     N = FILECHECK("", 1): EXIT SUB
    END IF
  CASE 5
   PASTEFROM
   EXIT SUB
  CASE 6
   REDIM WKIMG(32002)
   DEF SEG = VARSEG(WKIMG(0)): BLOAD "IMG.TMP", 0
   DEF SEG = VARSEG(Pal(0)): BLOAD "PAL.TMP", 0
   SETHUES Pal(), 0, 255: PUT (0, 0), WKIMG, PSET
   KILL "IMG.TMP": KILL "PAL.TMP"
   OPT(1) = O1R: SW = SSW: SH = SSH
   SSH = 0: SSW = 0: O1R = 0: SPRCONFIGS 1, 0
   EXIT SUB
 END SELECT

 REDIM WKIMG(32002): GET (0, 0)-(319, 199), WKIMG

END SUB

SUB DRAWBORDERS (c)

 X = XST: Y = YST
 FOR Y1 = 1 TO MYI: FOR X1 = 1 TO MXI
  LINE (X, Y)-((X + XSTP), (Y + YSTP)), c, B: X = X + XSTP
 NEXT: Y = Y + YSTP: X = XST: NEXT

END SUB

SUB DRAWFUNC (MD)

 IF MD = 1 OR MD = 3 THEN
  SELECTPOINT 1: IF EXT THEN EXIT SUB
 ELSE
  PUT (0, 0), WKIMG, PSET: RESETMOUSE
 END IF

 SELECT CASE MD
  CASE 1
   EX = SX: EY = SY
   DO
    MOUSEFUNC 3, 0, 0
    IF MX <> EX OR MY <> EY THEN
     EX = MX: EY = MY
     PUT (0, 0), WKIMG, PSET: LINE (SX, SY)-(EX, EY), 115
    END IF
    IF CLEFT THEN
     CLICKWAIT
     LINE (SX, SY)-(EX, EY), CLR: GET (0, 0)-(319, 199), WKIMG
     SX = EX: SY = EY
    END IF
   LOOP UNTIL CRGHT
   SMX = (MX * 2): SMY = MY
  CASE 2
   SX = MX: SY = MY
   DO
    MOUSEFUNC 3, 0, 0: PSET (SX, SY), 115
    IF MX <> SX OR MY <> SY THEN
     SX = MX: SY = MY: PUT (0, 0), WKIMG, PSET
    END IF
    IF CLEFT THEN
     DO
      MOUSEFUNC 3, 0, 0: LINE (SX, SY)-(MX, MY), CLR
      GET (0, 0)-(319, 199), WKIMG: SX = MX: SY = MY
     LOOP WHILE CLEFT
    END IF
   LOOP UNTIL CRGHT
   SMX = (MX * 2): SMY = MY
  CASE 3
   SELECTAREA 320, 200: IF EXT THEN EXIT SUB
   SOLID = ASK(" SOLID? (Y/N) ")
   IF SOLID THEN
    LINE (SX, SY)-(EX, EY), CLR, BF
   ELSE
    LINE (SX, SY)-(EX, EY), CLR, B
   END IF
   GET (0, 0)-(319, 199), WKIMG
   SMX = (SX * 2): SMY = SY
  CASE 4
   ELP = ASK(" ELLIPSE? (Y/N) ")
   IF ELP THEN
    XS = INPUTNUM("X RADIUS?", 2): IF XS < 1 THEN XS = 1
    YS = INPUTNUM("Y RADIUS?", 2): IF YS < 1 THEN YS = 1
   ELSE
    XS = 1: YS = 1
   END IF
   SELECTPOINT 1: CLX = SX: CLY = SY: CS = 1
   IF EXT THEN EXIT SUB
   DO
    MOUSEFUNC 3, 0, 0
    IF MX <> SX THEN
     SX = MX: SY = MY: PUT (0, 0), WKIMG, PSET
     CS = SX - CLX: IF CS < 1 THEN CS = 1
     IF ELP THEN
      CIRCLE (CLX, CLY), CS, 115, , , (YS / XS)
     ELSE
      CIRCLE (CLX, CLY), CS, 115
     END IF
    END IF
    IF CRGHT THEN
     CLICKWAIT
     EXIT SUB
    END IF
   LOOP UNTIL CLEFT
   CLICKWAIT
   SOLID = ASK(" SOLID? (Y/N) ")
   IF ELP THEN
    CIRCLE (CLX, CLY), CS, CLR, , , (YS / XS)
   ELSE
    CIRCLE (CLX, CLY), CS, CLR
   END IF
   IF SOLID THEN PAINT (CLX, CLY), CLR, CLR
   GET (0, 0)-(319, 199), WKIMG
   SMX = (CLX * 2): SMY = CLY
  CASE 5
   REDIM SPRAY(59), SPBG(62)
   DEF SEG = VARSEG(SPRAY(0)): BLOAD "QBP003.DAT", 0
   W = SPRAY(0) \ 8: H = SPRAY(1): PP = 4: MV = 1
   FOR Y = 1 TO H: FOR X = 1 TO W
    DEF SEG = VARSEG(SPRAY(0))
    IF PEEK(PP) <> 0 THEN POKE PP, CLR
    PP = PP + 1
   NEXT: NEXT
   MOUSEFUNC 8, 0, 199 - 10: MOUSEFUNC 7, 0, (319 * 2) - (10 * 2)
   SX = MX: SY = MY
   DO
    MOUSEFUNC 3, 0, 0: IF MX <> SX OR MY <> SY THEN MV = 1
    IF MV THEN
     SX = MX: SY = MY: MV = 0: PUT (0, 0), WKIMG, PSET
     GET (SX, SY)-(SX + (W - 1), SY + (H - 1)), SPBG: PP = 4
     FOR Y = 1 TO H: FOR X = 1 TO W
       DEF SEG = VARSEG(SPRAY(0)): DAT = PEEK(PP)
       IF DAT <> 0 THEN DEF SEG = VARSEG(SPBG(0)): POKE PP, DAT
       PP = PP + 1
     NEXT: NEXT: PUT (SX, SY), SPBG, PSET
    END IF
    IF CLEFT THEN GET (0, 0)-(319, 199), WKIMG
   LOOP UNTIL CRGHT
   SMX = (MX * 2): SMY = MY
 END SELECT

 CLICKWAIT

END SUB

SUB EDITPALETTE

 PUT (0, 0), WKIMG, PSET: REDIM UPAL(255) AS HUES
 SETHUES UPAL(), 0, 255: IF OPT(1) THEN DRAWBORDERS 0
 REDIM TEMP(107 * 67 \ 2 + 2): GET (0, 0)-(106, 66), TEMP: PP& = 4
 FOR Y = 0 TO 199 STEP 3: FOR X = 0 TO 319 STEP 3
   DAT = POINT(X, Y)
   DEF SEG = VARSEG(TEMP(0)): POKE PP&, DAT
   PP& = PP& + 1
 NEXT: NEXT
 CLS : PUT (190, 104), TEMP, PSET: ERASE TEMP
 REDIM ICON(759): DEF SEG = VARSEG(ICON(0)): BLOAD "QBP005.DAT", 0
 PUT (284, 180), ICON(4 * 152), PSET: PUT (185, 82), ICON(0 * 152), PSET
 PUT (217, 82), ICON(1 * 152), PSET: PUT (248, 82), ICON(2 * 152), PSET
 PUT (284, 82), ICON(3 * 152), PSET: ERASE ICON
 LINE (188, 102)-(298, 172), 117, B: LINE (0, 10)-(162, 172), 117, B
 LINE (0, 176)-(259, 196), 117, B: LINE (188, 10)-(218, 75), 117, B
 LINE (228, 10)-(258, 75), 117, B: LINE (268, 10)-(298, 75), 117, B
 UPDATEPAL 0, 0, 0: X1 = 2: X = 2: Y = 12: R = 1
 FOR c = 0 TO 255
  IF c = CLR THEN
   CX = X - 1: CY = Y - 1: LINE (CX, CY)-(CX + 10, CY + 10), 112, BF
  END IF
  UPAL(c).RED = Pal(c).RED: UPAL(c).GRN = Pal(c).GRN
  UPAL(c).BLU = Pal(c).BLU: LINE (X, Y)-(X + 8, Y + 8), c, BF
  LINE (X1, 178)-(X1, 194), c: X = X + 10: R = R + 1: X1 = X1 + 1
  IF R > 16 THEN R = 1: X = 2: Y = Y + 10
 NEXT
 SETHUES Pal(), 0, 255: O1R = OPT(1)
 O1R = OPT(1): OPT(1) = 0: RESETMOUSE
 MOUSEFUNC 4, (CX + 5) * 2, CY + 4: MOUSEFUNC 1, 0, 0
 DO
  DO
   MOUSEFUNC 3, 0, 0: K$ = UCASE$(INKEY$): IF K$ <> "" THEN EXT = 1: EXIT DO
  LOOP UNTIL CLEFT OR CRGHT
  IF EXT THEN MOUSEFUNC 2, 0, 0: EXIT DO
  IF CLEFT THEN
   CLICKWAIT
   MOUSEFUNC 2, 0, 0
   IF MX < 173 THEN
    IF MX > 1 AND MX < 161 AND MY > 11 AND MY < 171 THEN
     CLR = POINT(MX, MY): X1 = 2: X = 2: Y = 12: R = 1
     LINE (CX, CY)-(CX + 10, CY + 10), 0, B
     FOR c = 0 TO 255
      IF c = CLR THEN
       CX = X - 1: CY = Y - 1: LINE (CX, CY)-(CX + 10, CY + 10), 112, B
      END IF
      X = X + 10: R = R + 1: X1 = X1 + 1
      IF R > 16 THEN R = 1: X = 2: Y = Y + 10
     NEXT
     UPDATEPAL 0, 0, 0
    END IF
   ELSE
    IF MY > 10 AND MY < 75 THEN
     IF MX > 189 AND MX < 217 THEN
      Pal(CLR).RED = 74 - MY: UPDATEPAL 0, 0, 0
     END IF
     IF MX > 229 AND MX < 257 THEN
      Pal(CLR).GRN = 74 - MY: UPDATEPAL 0, 0, 0
     END IF
     IF MX > 269 AND MX < 297 THEN
      Pal(CLR).BLU = 74 - MY: UPDATEPAL 0, 0, 0
     END IF
     SETHUES Pal(), CLR, CLR
    END IF
    IF MY > 82 AND MY < 96 THEN
     IF MX > 185 AND MX < 204 THEN
      ST = CLR: LINE (187, 91)-(202, 94), CLR, BF
     END IF
     IF MX > 248 AND MX < 267 THEN
      ET = CLR: LINE (250, 91)-(265, 94), CLR, BF
     END IF
     IF MX > 284 AND MX < 303 THEN
      UPDATEPAL 1, ST, ET: SETHUES Pal(), ST, ET: UPDATEPAL 0, 0, 0
     END IF
    ELSEIF MY > 180 AND MY < 194 THEN
     IF MX > 284 AND MX < 303 THEN EXIT DO
    END IF
   END IF
   MOUSEFUNC 1, 0, 0
  END IF
  IF CRGHT THEN
   CLICKWAIT
   MOUSEFUNC 2, 0, 0
   IF MX > 1 AND MX < 161 AND MY > 11 AND MY < 171 THEN
    CC = POINT(MX, MY)
    Pal(CC).RED = Pal(CLR).RED: Pal(CC).GRN = Pal(CLR).GRN
    Pal(CC).BLU = Pal(CLR).BLU: SETHUES Pal(), CC, CC
   END IF
   MOUSEFUNC 1, 0, 0
  END IF
 LOOP
 PUT (0, 0), WKIMG, PSET
 KEEP = ASK(" KEEP CHANGES IN PALETTE? (Y)ES (N)O ")
 IF KEEP = 0 THEN
  FOR c = 0 TO 255: Pal(c).RED = UPAL(c).RED
   Pal(c).GRN = UPAL(c).GRN: Pal(c).BLU = UPAL(c).BLU
  NEXT: SETHUES Pal(), 0, 255
 END IF
 OPT(1) = O1R: O1R = 0: PUT (0, 0), WKIMG, PSET

END SUB

FUNCTION FILECHECK (F$, MD)

 IF MD THEN
  LINE (0, 0)-(319, 7), 0, BF
  LOCATE 1, 4: PRINT "? FILE NOT FOUND, PRESS ANY KEY..."
  WHILE UCASE$(INKEY$) = "": WEND
 ELSE
  FS& = 0: ON ERROR GOTO FNF1
  OPEN F$ FOR INPUT AS #1: FS& = LOF(1): CLOSE #1
  IF FS& THEN FILECHECK = 1 ELSE FILECHECK = 0
 END IF

END FUNCTION

SUB FINDCOLOR (MD)

 SELECT CASE CLR
  CASE 0 TO 63: PST = 0: CASE 64 TO 127: PST = 64
  CASE 128 TO 191: PST = 128: CASE 192 TO 255: PST = 192
 END SELECT

 X = 150: Y = 72: R = 1
 FOR c = PST TO PST + 63
  IF c = CLR THEN
   CX = X - 1: CY = Y - 1
   IF MD THEN
    LINE (CX, CY)-(CX + 6, CY + 11), 112, B
   ELSE
    LINE (CX, CY)-(CX + 6, CY + 11), 0, B
   END IF
  END IF
  LINE (X, Y)-(X + 4, Y + 9), c, BF
  X = X + 6: R = R + 1: IF R > 16 THEN R = 1: X = 150: Y = Y + 11
 NEXT

END SUB

FUNCTION INPUTNAME$ (FSPC$)

 LINE (0, 0)-(319, 7), 0, BF
 VAL$ = "ABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789_-"

 LOCATE 1, 27: PRINT "CTRL-D=DISPLAY"
 
 DO
  K$ = UCASE$(INKEY$)
  IF INSTR(VAL$, K$) AND LEN(N$) < 8 THEN N$ = N$ + K$
  IF K$ = CHR$(8) AND LEN(N$) THEN N$ = LEFT$(N$, LEN(N$) - 1)
  IF K$ = CHR$(4) THEN
   LINE (0, 8)-(319, 199), 0, BF: ON ERROR GOTO NFF
   LOCATE 4, 1: FILES FSPC$
  END IF
  IF K$ = CHR$(27) THEN EXT = 1: EXIT FUNCTION
  LOCATE 1, 1: PRINT "FILENAME->"; N$; "_ "
 LOOP UNTIL K$ = CHR$(13)

 IF LEN(N$) < 1 THEN EXT = 1
 INPUTNAME$ = N$ + RIGHT$(FSPC$, 4)

END FUNCTION

FUNCTION INPUTNUM (M$, L)

 REDIM TMP(1282): GET (0, 0)-(319, 7), TMP
 V$ = "0123456789": LOCATE 1, 1: PRINT SPACE$(LEN(M$) + L + 2)

 DO
  K$ = UCASE$(INKEY$)
  IF INSTR(V$, K$) AND LEN(N$) < L THEN N$ = N$ + K$
  IF K$ = CHR$(8) AND LEN(N$) THEN N$ = LEFT$(N$, LEN(N$) - 1)
  LOCATE 1, 1: PRINT M$; N$; "_ "
 LOOP UNTIL K$ = CHR$(13)

 PUT (0, 0), TMP, PSET: ERASE TMP: INPUTNUM = VAL(N$)

END FUNCTION

SUB INTIALIZE

 MDAT$ = SPACE$(57)
 REDIM DAT$(57), MENU$(35), OPT(3)
 REDIM Pal(255) AS HUES, WKIMG(32002)

 OPEN "QBP004.DAT" FOR INPUT AS #1
  FOR i = 1 TO 57
    INPUT #1, DAT$(i): MC$ = CHR$(VAL(DAT$(i))): MID$(MDAT$, i, 1) = MC$
  NEXT
  FOR i = 0 TO 35: INPUT #1, MENU$(i): NEXT
 CLOSE #1

 MOUSEFUNC MF, 0, 0
 IF NOT MF THEN
  PRINT "MOUSE NOT FOUND..."
  WHILE UCASE$(INKEY$) = "": WEND: SYSTEM
 END IF

 DEF SEG = VARSEG(Pal(0)): BLOAD "QBP001.DAT", 0
 SETHUES Pal(), 0, 255: GET (0, 0)-(319, 199), WKIMG

 MX = 159: MY = 99: SW = 20: SH = 20
 SPRCONFIGS 1, 0: MENUX = 81: MENUY = 77
 SMX = (159 * 2): SMY = 99: CLR = 1: COLOR 120

END SUB

SUB MOUSEFUNC (MD, X1, X2)

 DEF SEG = VARSEG(MDAT$): MDAT = SADD(MDAT$)

 CALL ABSOLUTE(MD, BS, X1, X2, MDAT)

 IF MD = 3 THEN
  CLEFT = ((BS AND 1) <> 0): CRGHT = ((BS AND 2) <> 0): MX = X1 \ 2: MY = X2
 END IF

END SUB

SUB OPTIONS (MD)

 SELECT CASE MD
  CASE 1
   OPT(1) = OPT(1) + 1: IF OPT(1) > 1 THEN OPT(1) = 0
   IF OPT(1) = 0 THEN
    PUT (0, 0), WKIMG, PSET: DRAWBORDERS 0: GET (0, 0)-(319, 199), WKIMG
   END IF
  CASE 2 TO 3
   OPT(MD) = OPT(MD) + 1: IF OPT(MD) > 1 THEN OPT(MD) = 0
  CASE 4
   ARRANGE
  CASE 5
   IF OPT(1) THEN
    PUT (0, 0), WKIMG, PSET
    CSWH = ASK(" CHANGE SPRITE W/H? (Y/N) ")
    IF CSWH THEN
     DO: SW = INPUTNUM("NEW WIDTH->", 3)
     LOOP UNTIL SW > 1 AND SW <= 100
     DO: SH = INPUTNUM("NEW HEIGHT->", 3)
     LOOP UNTIL SH > 1 AND SH <= 100
     CLS : GET (0, 0)-(319, 199), WKIMG: SPRCONFIGS 1, 0
    END IF
   END IF
  CASE 6
   IF OPT(1) = 0 THEN EXIT SUB
   PUT (0, 0), WKIMG, PSET: SPRCONFIGS 2, 0
   REDIM CELL(99): RESETMOUSE
   DO
    SELECTPOINT 0: IF EXT THEN EXIT DO
    SPRCONFIGS 0, 1: CELL(N) = SN: N = N + 1
   LOOP UNTIL N = UBOUND(CELL) + 1
   DLY& = INPUTNUM("DELAY->", 2): MAX = N - 1
   X = 160 - (SW \ 2): Y = 90 - (SH \ 2)
   DLY& = DLY& * 1000: SS& = (SW * SH) \ 2 + 2
   LINE ((X - 5), (Y - 5))-(X + (SW + 5), Y + (SH + 4)), 0, BF
   LINE ((X - 5), (Y - 5))-(X + (SW + 5), Y + (SH + 4)), 112, B
   PUT (X, Y), SPR(CELL(0) * SS&), PSET: N = 0
   DO
    PUT (X, Y), SPR(CELL(N) * SS&), PSET
    N = N + 1: IF N > MAX THEN N = 0
    FOR T& = 1 TO DLY&: NEXT
    K$ = UCASE$(INKEY$): MOUSEFUNC 3, 0, 0
   LOOP UNTIL CLEFT OR CRGHT OR K$ <> ""
   CLICKWAIT
   CLS : X = XST + 1: Y = YST + 1: R = 1
   FOR i = 0 TO UBOUND(SPR) - SS& STEP SS&
    PUT (X, Y), SPR(i), PSET: X = X + XSTP: R = R + 1
    IF R > MXI THEN R = 1: X = XST + 1: Y = Y + YSTP
   NEXT: DRAWBORDERS 120: ERASE SPR
   REDIM WKIMG(32002): GET (0, 0)-(319, 199), WKIMG
 END SELECT

END SUB

SUB PAINTFUNC (MD)

 IF MD <> 3 THEN
  IF MD = 2 AND OPT(1) THEN EXIT SUB
  SELECTPOINT 1: IF EXT THEN EXIT SUB
 END IF

 SELECT CASE MD
  CASE 1
   REDIM TMP(1282)
   IF OPT(3) THEN PUT (0, 0), WKIMG, PSET
   GET (0, 0)-(319, 7), TMP: PAINT (SX, SY), CLR, CLR
   KEEP = ASK(" KEEP FILL? (Y/N) ")
   IF KEEP THEN
    GET (0, 0)-(319, 199), WKIMG
   ELSE
    PUT (0, 0), TMP, PSET: ERASE TMP
   END IF
  CASE 2
   SELECTAREA 320, 200: IF EXT THEN EXIT SUB
   X = SX: Y = SY
   MOUSEFUNC 8, SY, EY: MOUSEFUNC 7, (SX * 2), (EX * 2)
   SELECTPOINT 0: PUT (0, 0), WKIMG, PSET
   RC = POINT(SX, SY)
   FOR Y1 = Y TO EY: FOR X1 = X TO EX
     IF POINT(X1, Y1) = RC THEN PSET (X1, Y1), CLR
   NEXT: NEXT
   GET (0, 0)-(319, 199), WKIMG: SMX = (SX * 2): SMY = SY
  CASE 3
   PUT (0, 0), WKIMG, PSET: LINE (0, 0)-(319, 199), 0, BF
   GET (0, 0)-(319, 199), WKIMG
 END SELECT

END SUB

SUB PALETTEFUNC (MD)

 SELECT CASE MD
  CASE 1
   F$ = INPUTNAME$("*.PAL"): IF EXT THEN EXIT SUB
   FF = FILECHECK(F$, 0)
   IF FF THEN
    DEF SEG = VARSEG(Pal(0)): BLOAD F$, 0: SETHUES Pal(), 0, 255
   ELSE
    N = FILECHECK("", 1)
   END IF
  CASE 2
   F$ = INPUTNAME$("*.PAL"): IF EXT THEN EXIT SUB
   FF = FILECHECK(F$, 0): OV = 1
   IF FF THEN
    LINE (0, 0)-(319, 7), 0, BF: OV = ASK(" OVERWRITE? (Y/N) ")
   END IF
   IF OV THEN DEF SEG = VARSEG(Pal(0)): BSAVE F$, 0, 1536
  CASE 3
   F$ = INPUTNAME$("*.PAL"): IF EXT THEN EXIT SUB
   FF = FILECHECK(F$, 0): OV = 1: IF FF THEN KILL F$
  CASE 4: EDITPALETTE
 END SELECT

END SUB

SUB PASTE (SOURCE(), MD)

 WTH& = SOURCE(0) \ 8: HGT& = SOURCE(1): MOUSEFUNC 4, (SX * 2), SY

 IF OPT(1) THEN
  RESETMOUSE
  SELECTPOINT 0: IF EXT THEN EXIT SUB
  SPRCONFIGS 0, 0
 ELSE
  DO
   MOUSEFUNC 3, 0, 0: K$ = UCASE$(INKEY$): CHK.XY
   IF K$ = "1" OR K$ = "2" OR K$ = "3" THEN MD = VAL(K$) - 1
   SMX = (SX * 2): SMY = MY
   LINE (SX, SY)-((SX + WTH&) - 1, (SY + HGT&) - 1), 115, B
   IF MX <> SX OR MY <> SY THEN
    SX = MX: SY = MY: PUT (0, 0), WKIMG, PSET
   END IF
   IF CLEFT AND CRGHT OR K$ = CHR$(27) THEN EXT = 1: CLICKWAIT: EXIT SUB
   IF CRGHT THEN
    PUT (0, 0), WKIMG, PSET: PUT (SX, SY), SOURCE, XOR
    DO: MOUSEFUNC 3, 0, 0: LOOP WHILE CRGHT: PUT (0, 0), WKIMG, PSET
   END IF
  LOOP UNTIL CLEFT
 END IF
 CLICKWAIT
 PUT (0, 0), WKIMG, PSET

 IF MD = 0 THEN
  PUT (SX, SY), SOURCE, PSET
 ELSE
  ERASE WKIMG: SIZ& = ((WTH& * HGT&) \ 2) + 2
  REDIM DESTINATION(SIZ&)
  GET (SX, SY)-(SX + WTH& - 1, SY + HGT& - 1), DESTINATION
  DPOS& = 4: SPOS& = 4
  DSeg = VARSEG(DESTINATION(0))
  SSEG = VARSEG(SOURCE(0))
  SELECT CASE MD
   CASE 1
    FOR Y1 = 1 TO HGT&: FOR X1 = 1 TO WTH&
      DEF SEG = SSEG: DAT = PEEK(SPOS&)
      IF DAT > 0 THEN DEF SEG = DSeg: POKE DPOS&, DAT
      SPOS& = SPOS& + 1: DPOS& = DPOS& + 1
    NEXT: NEXT
   CASE 2
    FOR Y1 = 1 TO HGT&: FOR X1 = 1 TO WTH&
      DEF SEG = SSEG: DAT = PEEK(SPOS&): DEF SEG = DSeg
      IF PEEK(DPOS&) = 0 THEN : DEF SEG = DSeg: POKE DPOS&, DAT
      SPOS& = SPOS& + 1: DPOS& = DPOS& + 1
    NEXT: NEXT
  END SELECT
  PUT (SX, SY), DESTINATION, PSET: ERASE DESTINATION: REDIM WKIMG(32002)
 END IF

 GET (0, 0)-(319, 199), WKIMG

END SUB

SUB PASTEFROM

 IT = PDMENU(34, 2): IF EXT THEN EXIT SUB
 IF IT = 1 THEN FSPC$ = "*.QBI" ELSE FSPC$ = "*.QBS": SP = 1
 PUT (0, 0), WKIMG, PSET: F$ = INPUTNAME$(FSPC$): IF EXT THEN EXIT SUB
 FF = FILECHECK(F$, 0)
 IF FF THEN
  CLS
  SSW = SW: SSH = SH: O1R = OPT(1): OPT(1) = 0
  DEF SEG = VARSEG(WKIMG(0)): BSAVE "IMG.TMP", 0, 64004
  DEF SEG = VARSEG(Pal(0)): BSAVE "PAL.TMP", 0, 1536
  SFS& = ((FS& - 7) \ 2 - 1): REDIM WKIMG(SFS&)
  DEF SEG = VARSEG(WKIMG(0)): BLOAD F$, 0
  F$ = LEFT$(F$, LEN(F$) - 3) + "PAL": LP = FILECHECK(F$, 0)
  IF LP THEN
   DEF SEG = VARSEG(Pal(0)): BLOAD F$, 0: SETHUES Pal(), 0, 255
  END IF
  IF SP THEN
   SW = WKIMG(0) \ 8: SH = WKIMG(1)
   SPRCONFIGS 1, 0: DRAWBORDERS 120
   SS& = (SW * SH) \ 2 + 2: N = 0
   X = XST + 1: Y = YST + 1: R = 1
   DO
    PUT (X, Y), WKIMG(N), PSET
    X = X + XSTP: R = R + 1: N = N + SS&
    IF R > MXI THEN R = 1: X = XST + 1: Y = Y + YSTP
   LOOP WHILE N < SFS&
   ELSE
    'BLKPUT 1, 0, 0, WKIMG(0)
    PUT (0, 0), WKIMG, PSET
   END IF
   REDIM WKIMG(32002): GET (0, 0)-(319, 199), WKIMG
   SELECTPOINT 1: IF EXT THEN DISKFUNC 6: EXIT SUB
   SELECTAREA 320, 200: IF EXT THEN DISKFUNC 6: EXIT SUB
   W& = (EX + 1) - SX: H& = (EY + 1) - SY
   PF& = (W& * H&) \ 2 + 2
   IF PF& > 27002 THEN DISKFUNC 6: EXIT SUB
   PUT (0, 0), WKIMG, PSET
   REDIM PFRM(PF&): GET (SX, SY)-(EX, EY), PFRM
   CLS : DISKFUNC 6: RESETMOUSE
   O1R = OPT(1): OPT(1) = 0
   MOUSEFUNC 8, 0, (200 - H&): MOUSEFUNC 7, 0, (320 * 2) - (W& * 2)
   PASTE PFRM(), 0: ERASE PFRM
   OPT(1) = O1R: O1R = 0: EXIT SUB
  ELSE
   N = FILECHECK("", 1): EXIT SUB
  END IF

END SUB

FUNCTION PDMENU (MS, ML)

 IF MS = 23 THEN OM = 1
 LINE (71, 70)-(153, 73 + (ML * 8)), 0, BF

 Y = 10: O = 1
 IF OM THEN
  REDIM ONOFF$(1): ONOFF$(0) = "OFF": ONOFF$(1) = "ON "
  FOR i = 23 TO 25
   LOCATE Y, 10: PRINT MENU$(i) + ONOFF$(OPT(O)): O = O + 1: Y = Y + 1
  NEXT
  FOR i = 26 TO 28: LOCATE Y, 10: PRINT MENU$(i): Y = Y + 1: NEXT
  ERASE ONOFF$
 ELSE
  FOR i = MS TO MS + (ML - 1): LOCATE Y, 10: PRINT MENU$(i): Y = Y + 1: NEXT
 END IF

 LINE (72, 70)-(152, 72 + (ML * 8)), 112, B

 MOUSEFUNC 7, (75 * 2), (75 * 2): MOUSEFUNC 8, 75, 76 + ((ML - 1) * 8)
 MY = 76: MOUSEFUNC 4, MX, MY: MOUSEFUNC 1, 0, 0

 DO
  MOUSEFUNC 3, 0, 0: SLCT = ((MY - 72) \ 8) + 1
  IF CRGHT THEN EXT = 1: CLICKWAIT: MOUSEFUNC 2, 0, 0: EXIT FUNCTION
 LOOP UNTIL CLEFT

 CLICKWAIT
 MOUSEFUNC 2, 0, 0: PDMENU = SLCT

END FUNCTION

SUB QUICKCOPY (SPM%)

 MOUSEFUNC 2, 0, 0: SPRCONFIGS 0, 1
 W = EX + 1 - SX: H = EY + 1 - SY: REDIM CLPBRD((W * H) \ 2 + 2)
 GET (SX, SY)-(EX, EY), CLPBRD: R = OPT(4): OPT(4) = 0
 SMX = (MX * 2): SMY = MY: PASTE CLPBRD(), SPM: OPT(4) = R
 IF EXT THEN
  EXT = 0: PUT (0, 0), WKIMG, PSET
  MOUSEFUNC 1, 0, 0: ERASE CLPBRD: EXIT SUB
 END IF
 PUT (0, 0), WKIMG, PSET: SPRCONFIGS 0, 0
 PUT (0, 0), WKIMG, PSET: PUT (SX, SY), CLPBRD, PSET

 ERASE CLPBRD: GET (0, 0)-(319, 199), WKIMG: MOUSEFUNC 1, 0, 0

END SUB

SUB RESETMOUSE

 IF OPT(1) THEN
  MH = YST + (YSTP * MYI) - 1
  MW = (XST * 2) + ((XSTP * 2) * MXI) - 1
  MOUSEFUNC 8, YST + 1, MH
  MOUSEFUNC 7, (XST * 2) + 2, MW
 ELSE
  MH = 199: MW = 319
  MOUSEFUNC 8, 0, MH: MOUSEFUNC 7, 0, (MW * 2)
 END IF

 MOUSEFUNC 4, SMX, SMY

END SUB

SUB SELECTAREA (MW, MH)

 IF SX + MW > 319 THEN MW1 = 319 ELSE MW1 = SX + MW
 IF SY + MH > 199 THEN MH1 = 199 ELSE MH1 = SY + MH

 MOUSEFUNC 8, SY, MH1: MOUSEFUNC 7, (SX * 2), (MW1 * 2)

 EX = SX: EY = SY: PSET (SX, SY), 115
 DO
  MOUSEFUNC 3, 0, 0
  LINE (SX, SY)-(EX, EY), 115, B
  IF OPT(2) THEN
   LOCATE 1, 15: PRINT "W:"; (EX + 1) - SX
   LOCATE 1, 22: PRINT "H:"; (EY + 1) - SY
  END IF
  IF MX <> EX OR MY <> EY THEN
   EX = MX: EY = MY: PUT (0, 0), WKIMG, PSET
  END IF
  IF CRGHT THEN EXT = 1: CLICKWAIT: EXIT SUB
 LOOP UNTIL CLEFT

 PUT (0, 0), WKIMG, PSET
 LINE (SX, SY)-(EX, EY), 115, B
 CLICKWAIT

END SUB

SUB SELECTPOINT (MD)

 IF MD = 1 THEN
   RESETMOUSE
   PUT (0, 0), WKIMG, PSET
 END IF

 MOUSEFUNC 1, 0, 0
 DO
  MOUSEFUNC 3, 0, 0: CHK.XY
  IF CRGHT THEN EXT = 1: CLICKWAIT: MOUSEFUNC 2, 0, 0: EXIT SUB
  SMX = (MX * 2): SMY = MY
 LOOP UNTIL CLEFT

 CLICKWAIT
 MOUSEFUNC 2, 0, 0: SX = MX: SY = MY

END SUB

SUB SETHUES (HUE() AS HUES, SC, EC)

 OUT &H3C8, SC

 FOR i = SC TO EC
   OUT &H3C9, HUE(i).RED: OUT &H3C9, HUE(i).GRN: OUT &H3C9, HUE(i).BLU
 NEXT

END SUB

SUB SHOWMENU

 REDIM TMP(6639)
 DEF SEG = VARSEG(TMP(0))
 BLOAD "QBP002.DAT", 0
 PUT (71, 43), TMP, PSET
 
 ERASE TMP: FINDCOLOR 1
 MOUSEFUNC 8, 71, 115: MOUSEFUNC 7, (73 * 2), (245 * 2)
 MOUSEFUNC 4, (MENUX * 2), MENUY

END SUB

SUB SPRCONFIGS (MD, MD1)

 SELECT CASE MD
  CASE 0             'CALC LOCATION
   IF OPT(1) THEN
    CL = (MX - XST) \ XSTP: RW = (MY - YST) \ YSTP
    SX = (CL * XSTP) + (XST + 1): SY = (RW * YSTP) + (YST + 1)
    EX = SX + (SW - 1): EY = SY + (SH - 1): SN = (RW * MXI) + CL
    IF MD1 THEN LINE ((SX - 1), (SY - 1))-((EX + 1), (EY + 1)), 112, B
   ELSE
    EXIT SUB
   END IF
  CASE 1             'CONFIGURE W & H
   XSTP = SW + 1: YSTP = SH + 1
   MXI = 319 \ XSTP: MYI = 199 \ YSTP
   XST = (319 MOD XSTP) \ 2: YST = (199 MOD YSTP) \ 2
  CASE 2             'CAPTURE SPRITES
   ERASE WKIMG: SS& = (SW * SH) \ 2 + 2
   TOT& = (MXI * MYI) * SS&: REDIM SPR(TOT&)
   X = XST + 1: Y = YST + 1
   FOR Y1 = 1 TO MYI: FOR X1 = 1 TO MXI
     GET (X, Y)-(X + (SW - 1), Y + (SH - 1)), SPR(N)
     N = N + SS&: X = X + XSTP
   NEXT: X = XST + 1: Y = Y + YSTP: NEXT
 END SELECT

END SUB

SUB TOOLFUNC (MD)

 IF MD < 4 AND OPT(1) <> 1 THEN
  SELECTPOINT 1: IF EXT THEN EXIT SUB
  SELECTAREA 320, 200: IF EXT THEN EXIT SUB
 ELSE
  PUT (0, 0), WKIMG, PSET: RESETMOUSE
 END IF

 PUT (0, 0), WKIMG, PSET: W = (EX + 1) - SX: H = (EY + 1) - SY

 SELECT CASE MD
  CASE 1
   REDIM FLIP(W): X = SX: Y = SY
   FOR H1 = 1 TO H
    GET (SX, Y)-(SX + W, Y), FLIP: PP& = 4 + W - 1
    FOR RDF = 1 TO W
     DEF SEG = VARSEG(FLIP(0)): DAT = PEEK(PP&)
     PSET (X, Y), DAT: X = X + 1: PP& = PP& - 1
   NEXT: X = SX: Y = Y + 1: NEXT: ERASE FLIP
  CASE 2
   REDIM FLIP(H): X = SX: Y = SY
   FOR W1 = 1 TO W
    GET (X, SY)-(X, SY + H - 1), FLIP: PP& = 4 + H - 1
    FOR RDF = 1 TO H
     DEF SEG = VARSEG(FLIP(0)): DAT = PEEK(PP&)
     PSET (X, Y), DAT: Y = Y + 1: PP& = PP& - 1
   NEXT: Y = SY: X = X + 1: NEXT: ERASE FLIP
  CASE 3
   IF OPT(1) AND W <> H THEN EXIT SUB
   W& = W: H& = H: ERASE WKIMG: REDIM ROT((W& * H&) \ 2 + 2)
   GET (SX, SY)-(EX, EY), ROT: X = SX + H - 1: Y = SY: PP& = 4
   LINE (SX, SY)-(EX, EY), 0, BF
   FOR Y1 = 1 TO H: FOR X1 = 1 TO W
     DEF SEG = VARSEG(ROT(0)): DAT = PEEK(PP&)
     PSET (X, Y), DAT: Y = Y + 1: PP& = PP& + 1
   NEXT: Y = SY: X = X - 1: NEXT: ERASE ROT: REDIM WKIMG(32002)
  CASE 4
   IF OPT(1) THEN EXIT SUB
   LINE (0, 0)-(319, 7), 0, BF
   V$ = "ABCDEFGHIJKLMNOPQRSTUVWXYZ 0123456789()*&^%$#@!{}[]\|';:.,<>?/=+-_`~"
   DO
    K$ = INKEY$
    IF K$ = CHR$(8) AND LEN(N$) THEN
     N$ = LEFT$(N$, LEN(N$) - 1)
    ELSEIF K$ = CHR$(27) THEN
     EXT = 1: EXIT SUB
    ELSE
     IF INSTR(V$, K$) OR INSTR(LCASE$(V$), K$) AND LEN(N$) < 38 THEN
      N$ = N$ + K$
     END IF
    END IF
    LOCATE 1, 1: PRINT N$; "_ "
   LOOP UNTIL K$ = CHR$(13)
   X = (LEN(N$) * 8) - 1: REDIM CLPBRD(((X + 1) * 8) \ 2 + 2)
   GET (0, 0)-(X, 7), CLPBRD: PUT (0, 0), WKIMG, PSET
   OV = ASK(" OVERLAY (Y/N) ")
   MOUSEFUNC 8, 0, (200 - 8): MOUSEFUNC 7, 0, (320 * 2) - ((X + 1) * 2)
   SX = 0: SY = 0: PASTE CLPBRD(), OV: EXIT SUB
  CASE 5
   IF OPT(1) = 0 THEN EXIT SUB
   REDIM CLPBRD((SW * SH) \ 2 + 2): GET (SX, SY)-(EX, EY), CLPBRD
   LINE ((SX - 1), (SY - 1))-((EX + 1), (EY + 1)), 112, B
   DO
    K$ = UCASE$(INKEY$)
    IF K$ = CHR$(27) OR K$ = CHR$(13) OR K$ = CHR$(32) THEN
     EXIT DO
    ELSE
     UD = 1
     SELECT CASE K$
      CASE CHR$(0) + "M": X = 1: Y = 0: UD = 1
      CASE CHR$(0) + "K": X = -1: Y = 0: UD = 1
      CASE CHR$(0) + "P": X = 0: Y = 1: UD = 1
      CASE CHR$(0) + "H": X = 0: Y = -1: UD = 1
      CASE ELSE: UD = 0
     END SELECT
    END IF
    IF UD THEN
     LINE (SX, SY)-(EX, EY), 0, B: PUT ((SX + X), (SY + Y)), CLPBRD, PSET
     LINE ((SX - 1), (SY - 1))-((EX + 1), (EY + 1)), 112, B
     GET (SX, SY)-(EX, EY), CLPBRD
    END IF
   LOOP
   KEEP = ASK(" KEEP CHANGES? (Y/N) ")
   IF KEEP = 0 THEN PUT (0, 0), WKIMG, PSET
 END SELECT

 GET (0, 0)-(319, 199), WKIMG

END SUB

SUB UPDATEPAL (Mode, ST, ET)

IF Mode THEN
 IF ST > ET THEN EXIT SUB ELSE TL = ET - ST
 IF TL > 16 OR TL < 2 THEN EXIT SUB
 RSTP = (Pal(ET).RED - Pal(ST).RED) / TL
 GSTP = (Pal(ET).GRN - Pal(ST).GRN) / TL
 BSTP = (Pal(ET).BLU - Pal(ST).BLU) / TL
 RH = Pal(ST).RED: GH = Pal(ST).GRN: BH = Pal(ST).BLU: TC = ST
 FOR i = 0 TO TL
  Pal(TC).RED = RH: Pal(TC).GRN = GH: Pal(TC).BLU = BH
  RH = RH + RSTP
  IF RH < 0 THEN RH = 0
  IF RH > 63 THEN RH = 63
  GH = GH + GSTP
  IF GH < 0 THEN GH = 0
  IF GH > 63 THEN GH = 63
  BH = BH + BSTP
  IF BH < 0 THEN BH = 0
  IF BH > 63 THEN BH = 63
  TC = TC + 1
 NEXT
ELSE
 LOCATE 1, 2: PRINT "COLOR:"; CLR; "  "
 LOCATE 1, 15: PRINT "RED:"; Pal(CLR).RED
 LOCATE 1, 24: PRINT "GRN:"; Pal(CLR).GRN
 LOCATE 1, 33: PRINT "BLU:"; Pal(CLR).BLU
 IF Pal(CLR).RED = 0 THEN
  LINE (189, 74)-(217, 11), 0, BF
 ELSE
  LINE (189, 74)-(217, 74 - Pal(CLR).RED), 39, BF
  IF Pal(CLR).RED < 63 THEN LINE (189, 73 - Pal(CLR).RED)-(217, 11), 0, BF
 END IF
 IF Pal(CLR).GRN = 0 THEN
  LINE (229, 74)-(257, 11), 0, BF
 ELSE
  LINE (229, 74)-(257, 74 - Pal(CLR).GRN), 71, BF
  IF Pal(CLR).GRN < 63 THEN LINE (229, 73 - Pal(CLR).GRN)-(257, 11), 0, BF
 END IF
 IF Pal(CLR).BLU = 0 THEN
  LINE (269, 74)-(297, 11), 0, BF
 ELSE
  LINE (269, 74)-(297, 74 - Pal(CLR).BLU), 103, BF
  IF Pal(CLR).BLU < 63 THEN LINE (269, 73 - Pal(CLR).BLU)-(297, 11), 0, BF
 END IF
END IF

END SUB

SUB ZOOMEDIT

 IF OPT(1) THEN
  PUT (0, 0), WKIMG, PSET: RESETMOUSE
 ELSE
  SELECTPOINT 1: IF EXT THEN EXT = 0: EXIT SUB
  SELECTAREA 100, 100: IF EXT THEN EXT = 0: EXIT SUB
 END IF

 PUT (0, 0), WKIMG, PSET: ZW = EX + 1 - SX: ZH = EY + 1 - SY
 REDIM ZIMG((ZW * ZH) \ 2 + 2): GET (SX, SY)-(EX, EY), ZIMG

 ZSEG = VARSEG(ZIMG(0)): XF = 220 \ ZW: YF = 200 \ ZH
 IF XF < YF THEN ZPS = XF ELSE ZPS = YF
 IF ZPS < 2 THEN ZPS = 2
 ZEX = ZW * ZPS: ZEY = ZH * ZPS: RM = (319 - ZEX) \ 2
 ZIX = (ZEX + RM) - (ZW \ 2) + 1: ZIY = 143 - ZH \ 2
 PSX = 1 + (ZEX + (RM - 47)): PEX = (PSX - 1) + 16 * 6
 REDIM BP(255) AS HUES: SETHUES BP(), 0, 255

 LINE (0, 0)-(319, 199), 30, BF: LINE (0, 0)-(ZW * ZPS, ZH * ZPS), 0, BF
 PUT (ZIX, ZIY), ZIMG, PSET
 LINE (ZIX - 1, ZIY - 1)-(ZIX + ZW, ZIY + ZH), 0, B
 LINE (PSX - 1, 5)-(PEX, 85), 0, BF

 X = PSX: Y = 6: R = 1
 FOR c = 0 TO 255
  IF c = CLR THEN
   CX = X - 1: CY = Y - 1: LINE (CX, CY)-(CX + 6, CY + 5), 112, B
  END IF
  LINE (X, Y)-(X + 4, Y + 3), c, BF: X = X + 6: R = R + 1
  IF R > 16 THEN R = 1: X = PSX: Y = Y + 5
 NEXT

 X = 0: Y = 0: PP = 4
 FOR H = 1 TO ZH: FOR W = 1 TO ZW
  DEF SEG = ZSEG: DAT = PEEK(PP)
  IF DAT THEN LINE (X, Y)-(X + ZPS - 1, Y + ZPS - 1), DAT, BF
  X = X + ZPS: PP = PP + 1
 NEXT: X = 0: Y = Y + ZPS: NEXT
 ERASE BP: SETHUES Pal(), 0, 255

 MX = 0: MY = 0
 MOUSEFUNC 8, 0, 199: MOUSEFUNC 7, 0, (319 * 2)
 MOUSEFUNC 4, 0, 0: MOUSEFUNC 1, 0, 0

 DO
  MOUSEFUNC 3, 0, 0: K$ = UCASE$(INKEY$)
  IF MX < ZEX AND MY < ZEY THEN
   CL = MX \ ZPS: RW = MY \ ZPS: PLOT = 1
   IF OPT(3) THEN
    LOCATE 1, 1: PRINT "X:"; CL + 1: LOCATE 1, 8: PRINT "Y:"; RW + 1
   END IF
  ELSE
   PLOT = 0
  END IF
  IF CLEFT AND CRGHT THEN
   CLICKWAIT
   MOUSEFUNC 2, 0, 0: RC = POINT(MX, MY)
   X = 0: Y = 0: PP = 4
   FOR H = 1 TO ZH: FOR W = 1 TO ZW
     DEF SEG = ZSEG: DAT = PEEK(PP)
     IF DAT = RC THEN
      DEF SEG = ZSEG: POKE PP, CLR
      LINE (X, Y)-(X + ZPS - 1, Y + ZPS - 1), CLR, BF
     END IF
     X = X + ZPS: PP = PP + 1
   NEXT: X = 0: Y = Y + ZPS: NEXT
   PUT (ZIX, ZIY), ZIMG, PSET: MOUSEFUNC 1, 0, 0
  END IF
  IF CLEFT THEN
   DEF SEG = ZSEG
   ZX = (CL * ZPS): ZY = (RW * ZPS): PP = (4 + (RW * ZW)) + CL
   IF PLOT AND PEEK(PP) <> CLR THEN
    MOUSEFUNC 2, 0, 0
    LINE (ZX, ZY)-(ZX + ZPS - 1, ZY + ZPS - 1), CLR, BF
    DEF SEG = ZSEG: POKE PP, CLR: PUT (ZIX, ZIY), ZIMG, PSET
    MOUSEFUNC 1, 0, 0
   ELSE
    IF MX > PSX AND MX < PEX AND MY > 5 AND MY < 85 THEN
     CLICKWAIT
     MOUSEFUNC 2, 0, 0: CLR = POINT(MX, MY): CUD = 1
    END IF
   END IF
  END IF
  IF CRGHT THEN
   IF MX < ZEX AND MY < ZEY THEN
    CLICKWAIT
    MOUSEFUNC 2, 0, 0: CLR = POINT(MX, MY): CUD = 1
   ELSE
    CLICKWAIT
    EXIT DO
   END IF
  END IF
  IF CUD THEN
   CUD = 0: LINE (CX, CY)-(CX + 6, CY + 5), 0, B
   X = PSX: Y = 6: R = 1
   FOR c = 0 TO 255
    IF c = CLR THEN
     CX = X - 1: CY = Y - 1: LINE (CX, CY)-(CX + 6, CY + 5), 112, B
    END IF
    X = X + 6: R = R + 1: IF R > 16 THEN R = 1: X = PSX: Y = Y + 5
   NEXT: MOUSEFUNC 1, 0, 0
  END IF
 LOOP WHILE K$ = ""

 MOUSEFUNC 2, 0, 0: PUT (0, 0), WKIMG, PSET: PUT (SX, SY), ZIMG, PSET

 KEEP = ASK(" KEEP CHANGES? (Y/N) ")
 IF KEEP THEN
  GET (0, 0)-(319, 199), WKIMG
 ELSE
  PUT (0, 0), WKIMG, PSET
 END IF

END SUB

